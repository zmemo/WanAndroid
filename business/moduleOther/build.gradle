def runAlone = ModuleConfig.isModuleOtherRunAlone.booleanValue()

if (runAlone) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Config.compileSdkVersion
    defaultConfig {
        if (runAlone) {
            applicationId Config.applicationId + project.name
        }
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode Config.versionCode
        versionName Config.versionName
        testInstrumentationRunner Config.androidJunitRunner
        multiDexEnabled true
    }
    sourceSets {
        main {
            if (runAlone) {
                manifest.srcFile 'src/main/manifest/application/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/manifest/module/AndroidManifest.xml'
            }
        }
    }
}

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

dependencies {
    // 调试
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation DepConfig.junit
    androidTestImplementation DepConfig.testJunit
    androidTestImplementation DepConfig.testEspresso

    kapt DepConfig.arouterCompiler
    api project(path: ':config:moduleBase')
}
